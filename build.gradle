plugins {
	id 'java'
	id 'org.springframework.boot' version '3.1.1'
	id 'io.spring.dependency-management' version '1.1.0'
	id 'com.diffplug.spotless' version "6.19.0"
}

group = 'healthy.lifestyle.backend'

java {
	sourceCompatibility = '17'
}

repositories {
	mavenCentral()
}

jar {
	archivesBaseName = 'app'
	enabled = false
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'

	// Database
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	runtimeOnly 'org.postgresql:postgresql'
	implementation 'org.liquibase:liquibase-core'

	// Logging
	implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.30'
	implementation group: 'ch.qos.logback', name: 'logback-core', version: '1.2.9'
	testImplementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.9'

	// Monitoring
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	runtimeOnly 'io.micrometer:micrometer-registry-prometheus'

	// Testing
	testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: '5.3.1'
	testImplementation "org.testcontainers:testcontainers:1.18.3"
	testImplementation "org.testcontainers:postgresql:1.18.3"
	testImplementation "org.testcontainers:junit-jupiter:1.18.3"
}

tasks.named('test') {
	useJUnitPlatform()
}

test {
	testLogging {
		events "passed", "skipped", "failed"
	}
}

spotless {
	format 'misc', {
		target '*.gradle', '*.md', '.gitignore'

		trimTrailingWhitespace()
		indentWithTabs()
		endWithNewline()
	}
	java {
		palantirJavaFormat("2.32.0")
		importOrder()
		removeUnusedImports()
		trimTrailingWhitespace()
		endWithNewline()
		formatAnnotations()
	}
}
